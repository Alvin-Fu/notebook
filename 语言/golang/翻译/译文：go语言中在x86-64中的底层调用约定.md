[原文链接](https://dr-knz.net/go-calling-convention-x86-64.html)
文中的字表示一个字节
frame pointer： 帧指针

## 简介：
本文分析go的编译器如何在x86-64目标中生成方法调用，参数传递和异常处理的代码。
这里没有分析编译器如何在内存中分配数据(函数的参数和返回值除外)、逃逸分析是怎么工作的、为什么编译器必须适应异步的垃圾回收器以及goroutine的处理如何影响代码的生成。
本文中的所有的代码都是使用freebsd/amd64目标go1.10.3执行的。汇编代码是使用`go tool objdump`生成的并且使用的是go的汇编语法。

## 参数、返回值和调用顺序
### 参数和返回值
**如何传递参数给函数并返回结果呢？**
让我们看一下这个简单的方法：
`func EmptyFunc() {}`
他的汇编代码：
```
EmptyFunc:
     0x480630              c3                      RET
```


在加一个返回值时：
`func EmptyFunc()int{	return 123 }`
他的汇编代码：
```
FuncConst:
  0x480630              48c74424087b000000      MOVQ $0x7b, 0x8(SP)
  0x480639              c3                      RET
```
对比汇编代码我们可以看到返回值是通过栈进行传递的，而不像大多数的原生的编译语言标准x86-64调用约定使用寄存器。
**对比C或者C++的汇编输出：**
```
FuncConst:
      movl    $123, %eax
      retq
```
这是将返回值传递到寄存器中

**如何做一个简单的参数传递的go方法？**
```
// Note: substracting z so we know which argument is which.
func FuncAdd(x,y,z int) int {
	return x + y - z 
}
```
汇编代码：
```
FuncAdd:
  0x480630              488b442408              MOVQ 0x8(SP), AX  // get arg x
  0x480635              488b4c2410              MOVQ 0x10(SP), CX // get arg y
  0x48063a              4801c8                  ADDQ CX, AX       // %ax <- x + y
  0x48063d              488b4c2418              MOVQ 0x18(SP), CX // get arg z
  0x480642              4801c8                  SUBQ CX, AX       // %ax <- x + y - z
  0x480645              4889442420              MOVQ AX, 0x20(SP) // return x+y-z
  0x48064a              c3                      RET
```
从上面可以看到函数的参数也是通过堆栈进行传递，而不是像其他语言那样通过寄存器。
我们可以看到参数是在堆栈的顶部，而返回值则位于堆栈的底部。
**对比C和C++的汇编代码：**
```
FuncAdd:
      leal    (%rdi,%rsi), %eax
      subl    %edx, %eax
      retq
```
可以看到参数是通过寄存器进行传递。准确的数量依赖于调用约定，但是在freebsd/amd64下最多可以在寄存器中传递6个参数，其他的参数都必须在堆栈中。
注意：在go的公开提议中使用寄存器进行参数传递`https://github.com/golang/go/issues/18597`。但是没有被官方采纳。

### 函数的调用：一个函数是如何被调用的？
如何将上面的函数FuncAdd调用的？
```
func DoCallAdd() int { return FuncAdd(1, 2, 3) }
```
**汇编代码：**
```
0x480650              64488b0c25f8ffffff      MOVQ FS:0xfffffff8, CX
0x480659              483b6110                CMPQ 0x10(CX), SP
0x48065d              7641                    JBE 0x4806a0
0x48065f              4883ec28                SUBQ $0x28, SP
0x480663              48896c2420              MOVQ BP, 0x20(SP)
0x480668              488d6c2420              LEAQ 0x20(SP), BP
0x48066d              48c7042401000000        MOVQ $0x1, 0(SP)
0x480675              48c744240802000000      MOVQ $0x2, 0x8(SP)
0x48067e              48c744241003000000      MOVQ $0x3, 0x10(SP)
0x480687              e8a4ffffff              CALL src.FuncAdd(SB)
0x48068c              488b442418              MOVQ 0x18(SP), AX
0x480691              4889442430              MOVQ AX, 0x30(SP)
0x480696              488b6c2420              MOVQ 0x20(SP), BP
0x48069b              4883c428                ADDQ $0x28, SP
0x48069f              c3                      RET
0x4806a0              e80ba5fcff              CALL runtime.morestack_noctxt(SB)
0x4806a5              eba9                    JMP src.DoCallAdd(SB)
```
这里有做了上面事情？
在这个函数的中心我们看到了我们想看到的：
```
0x48066d              48c7042401000000        MOVQ $0x1, 0(SP)     // set arg x
0x480675              48c744240802000000      MOVQ $0x2, 0x8(SP)   // set arg y
0x48067e              48c744241003000000      MOVQ $0x3, 0x10(SP)  // set arg z
0x480687              e8a4ffffff              CALL src.FuncAdd(SB) // call
0x48068c              488b442418              MOVQ 0x18(SP), AX    // get return value of FuncAdd
0x480691              4889442430              MOVQ AX, 0x30(SP)    // set return value of DoCallAdd
```
函数被调用之前，参数会被压栈，在被调用后，从被调用帧中检索返回值并复制到调用帧中。
但是，我们已经知道了函数在被调用的之前参数是会被压栈的，这个意味者其他函数要想调用其他函数的时候就必须确保有足够的栈空间来进行函数调用。下面的代码就是在腾出栈空间并恢复：
```
// Before the call: make space for callee.
0x48065f              4883ec28                SUBQ $0x28, SP
// After the call: restore stack pointer.
0x48069b              4883c428                ADDQ $0x28, SP
```
剩下的是什么呢？
由于go是存在panic的因此必须保持运行时系统展开堆栈的能力。对于每一个被激活记录中就必须保存他的入口的堆栈指针和被调用的堆栈指针之间的差异。这一类型的指针是被存放在BP寄存器中，正是我们看到的汇编代码：(这块说的就是恢复现场是如何做到的)
```
// Store the frame pointer of the caller into a known location in
// the current activation record.
0x480663              48896c2420              MOVQ BP, 0x20(SP)
// Store the address of the copy of the parent frame pointer
// into the new frame pointer.
0x480668              488d6c2420              LEAQ 0x20(SP), BP
```
这就是一个约定的格式，即BP中总是指向了一个帧指针的链表，其中BP的每一个连续值总是比当前帧（SP+0x20）的堆栈指针多32个字节。通过这种方式堆栈总是可以对成功的扩展。
最后一段代码又代表什么呢？
```
0x480650              64488b0c25f8ffffff      MOVQ FS:0xfffffff8, CX
0x480659              483b6110                CMPQ 0x10(CX), SP
0x48065d              7641                    JBE 0x4806a0
...
0x4806a0              e80ba5fcff              CALL runtime.morestack_noctxt(SB)
0x4806a5              eba9                    JMP src.DoCallAdd(SB)
```
go的runtime使用小堆栈作为一个优化：这也意味者一个goroutine总是以一个最小的堆栈开始，因此在一个运行的go程序中是可以同时跑很多的goroutine。然而，这也意味者在标准的小堆栈中不能递归的调用更多的函数，这样可能会导致崩溃。
因此在go中，每一个需要在堆栈上有激活记录的函数首先需要去检查当前goroutine的堆栈是否足够的大。通过比较当前goroutine的低位标记和当前的堆栈指针来实现的，低位标记存储在goroutine结构的偏移量16位（0x10）处，他总是可以被找到通过0xfffffff8

**比较C/C++中DoCallAdd如何工作的？**
```
DoCallAdd:
      movl    $3, %edx
      movl    $2, %esi
      movl    $1, %edi
      jmp     FuncAdd
```
这个参数的传递是在寄存器中，然后通过jmp-尾部调用将控制权转移给被调用者。这是有效的，因为FuncAdd的返回值变成了DoCallAdd的返回值。
堆栈指针是什么呢？可以看到DoCallAdd在C中不能告诉我们更多的信息，因为与go相反，他没有任何变量被保存在堆栈上，因此确实需要一个活动记录。一般来说如果不需要保存活动记录，也就不需要设置或者调整堆栈指针。
那么如果才能让C/C++的编译器保存一个活动记录呢？我们可以这样：
```
void other(int *x);
int DoCallAddX() { int x = 123; other(&x); return x; }
```
汇编代码：
```
DoCallAddX:
     subq    $24, %rsp       // make space
     leaq    12(%rsp), %rdi  // allocate x at address rsp+12
     movl    $123, 12(%rsp)  // store 123 into x
     call    other           // call other(&x)
     movl    12(%rsp), %eax  // load value from x
     addq    $24, %rsp       // restore stack pointer
     ret
```
可以看到%rsp是在函数进入是被设置，在函数结尾是被恢复。
那么异常处理呢？

### C/C++中的异常处理
上面的程序是被支持异常的C/C++编译器生成的。一般情况下编译器是不能确保被调用着不抛出异常的。然而在上面的代码中我们没有看到任何关于保存堆栈指针或者设置帧指针的内容。那么C/C++是如何在运行时处理堆栈展开的呢？

在ABI(应用程序二进制接口)中主要有两种方法实现异常传播的：
- “动态注册”，每一个激活记录中都有帧指针，组织成一个链表。这个可以使得堆栈展开的快速，在每个函数调用其他的函数的时候都有设置帧指针的开销。这个容易实现。
- 表格控制，编译器和汇编器在代码的旁边创建数据结构用于指示代码的记录对应的激活记录的大小。在GUN的工具链中这个被称为调用帧信息数据(CFI)。当一个异常被产生时，将通过加载这个表的数据以确定如何展开。这个使得异常传播速度变慢，但是一般情况会更快。
通常，如果异常常见并且使用控制流的语言使用动态注册，然而异常不常见则使用表驱动展开，以确保一般情况的效率。后一种选择在C/C++中是非常常见的。
有趣的是，go的设计者是反对在控制流中使用panic的，因此他们希望他们的语言是使用第二类，并且应实现表驱动展开。但是go的编译器一直在使用动态注册。也许表驱动没有被实现是因为实现过于复杂吧？

### 是否有调用被保存在寄存器中
在go中是否有寄存器被保存在寄存器中呢？go的编译器能够期望被调用者去使用寄存器吗，也就是说，除非是确实需要一般是不会的。
其他语言中调用其他方法时尽可能地使用寄存器来保存重要的信息并且尽量避免将临时变量压入堆栈中(避免在堆栈上出现活跃记录)。
让我们试试
```
func Intermediate() int {
  x := Other()
  x += Other()
  return x
}
```
go的编译器是否使用寄存器来保存X的值呢？
让我们看看：
```
Intermediate:
  [...]
  0x4806dd              e8ceffffff              CALL src.Other(SB)
  0x4806e2              488b0424                MOVQ 0(SP), AX
  0x4806e6              4889442408              MOVQ AX, 0x8(SP)
  0x4806eb              e8c0ffffff              CALL src.Other(SB)
  0x4806f0              488b442408              MOVQ 0x8(SP), AX
  0x4806f5              48030424                ADDQ 0(SP), AX
  0x4806f9              4889442420              MOVQ AX, 0x20(SP)
  [...]
```
可以看到go的编译器在调用期间没有总是将临时变量推入堆栈中。
让我们看看C/C++的编译器是怎么做的？
```
Intermediate:
      pushq   %rbx          // save %rbx from caller
      xorl    %eax, %eax
      call    other
      movl    %eax, %ebx    // use callee-save for intermediate result
      xorl    %eax, %eax
      call    other
      addl    %ebx, %eax    // use callee-save again
      popq    %rbx          // restore callee-save for caller
      ret
```
可以看到C/C++调用约定中就出现了很多用于保存临时变量的寄存器。在这个平台上至少包含了%rbx

## 指针和接口的成本
go通过虚函数表实现了指针类型和接口类型(类似于C++中的虚函数)。
是如何实现方法调用的呢？
### 指针
看下面的代码：
```
func UsePtr(x *int) int { return *x }
```
汇编代码：
```
UsePtr:
  0x480630              488b442408              MOVQ 0x8(SP), AX  // load x
  0x480635              488b00                  MOVQ 0(AX), AX    // load *x
  0x480638              4889442410              MOVQ AX, 0x10(SP) // return *x
  0x48063d              c3                      RET
```
可以看到指针的大小和int的大小是一样的，在参数结构中只使用一个字槽。返回值是一样的。
```
var x int
func RetPtr() *int { return &x }
func NilPtr() *int { return nil }
```
汇编代码：
```
RetPtr:
  0x480650              488d0581010c00          LEAQ src.x(SB), AX  // compute &x
  0x480657              4889442408              MOVQ AX, 0x8(SP)    // return &x
  0x48065c              c3                      RET
NilPtr:
  0x480660              48c744240800000000      MOVQ $0x0, 0x8(SP)  // return 0
  0x480669              c3                      RET
```
### 接口
考虑一下代码：
```
type Foo interface{ foo() }
func InterfaceNil() Foo { return nil }
```
汇编代码：
```
InterfaceNil:
  0x4805b0              0f57c0                  XORPS X0, X0
  0x4805b3              0f11442408              MOVUPS X0, 0x8(SP)
  0x4805b8              c3                      RET
```
可以看到接口的占的空间更大。在go的汇编中的X0表示x86中的%xmm0寄存器，这是一个16字节(128位)寄存器。
我们可以通过将接口参数当作返回值传递给函数进行测试：
```
func InterfacePass(Foo x) Foo { return x }
```
汇编代码：
```
InterfacePass:
  0x4805b0              488b442408              MOVQ 0x8(SP), AX
  0x4805b5              4889442418              MOVQ AX, 0x18(SP)
  0x4805ba              488b442410              MOVQ 0x10(SP), AX
  0x4805bf              4889442420              MOVQ AX, 0x20(SP)
  0x4805c4              c3                      RET
```
尽管只有一个参数和返回值编译器必须复制两个单词。接口的值实际上是一个虚函数表指针和一个值的组合。
### string和slices使用两个或者三个
除了指针和接口，go的编译器还有两个特殊的设计：
- 特殊类型string使用两个字：一个字表示的是当前内容的长度另一个字表示string开始的位置(指针)。支持在常量时间len()计算长度和转化为slice
- 特殊类型slices使用三个字：一个字表示容量，一个字表示长度，一个字表示指向第一个元素的指针。支持在常量时间计算len(),cap()和切片。
在string中没有cap的原因是因为在go中string是一个不变类型。

### 构建接口值
构造一个不为空的接口值需要一个虚函数表指针和一个值。
大多时候虚函数表的部分是静态的(因为被转换为接口类型的值一般为静态)。我们将忽略将一个接口转为另一个接口。
对于值部分，在go基于值得实际类型有多种实现方式。
最常见得是，由指针类型实现得接口，如下面得代码：
```
Copy
// Define the interface.
type Foo interface{ foo() }
// Define a struct type implementing the interface by pointer.
type foo struct{ x int }
func (*foo) foo() {}
// Define a global variable so we don't use the heap allocator.
var x foo

// Make an interface value.
func MakeInterface1() Foo { return &x }
```
汇编代码：
```
MakeInterface1:
    0x4805c0              488d05d9010400          LEAQ go.itab.*src.foo,src.Foo(SB), AX
    0x4805c7              4889442408              MOVQ AX, 0x8(SP)
    0x4805cc              488d0505f20b00          LEAQ src.x(SB), AX
    0x4805d3              4889442410              MOVQ AX, 0x10(SP)
    0x4805d8              c3                      RET
```
正如预料得一样：第一个字是虚函数表得地址，第二个是指向结构体得指针。
如果结构体通过值实现接口那么开销就会变得很大了。
```
// Define a struct type implementing the interface by value.
type bar struct{ x int }
func (bar) foo() {}
// Define a global variable so we don't use the heap allocator.
var y bar

// Make an interface value.
func MakeInterface2() Foo { return y }
```
汇编代码：
```
MakeInterface2:
    0x4805c0              64488b0c25f8ffffff      MOVQ FS:0xfffffff8, CX
    0x4805c9              483b6110                CMPQ 0x10(CX), SP
    0x4805cd              7648                    JBE 0x480617
    0x4805cf              4883ec28                SUBQ $0x28, SP
    0x4805d3              48896c2420              MOVQ BP, 0x20(SP)
    0x4805d8              488d6c2420              LEAQ 0x20(SP), BP
    0x4805dd              488d053c020400          LEAQ go.itab.src.bar,src.Foo(SB), AX
    0x4805e4              48890424                MOVQ AX, 0(SP)
    0x4805e8              488d05e9f10b00          LEAQ src.x(SB), AX
    0x4805ef              4889442408              MOVQ AX, 0x8(SP)
    0x4805f4              e8e7b2f8ff              CALL runtime.convT2I64(SB)
    0x4805f9              488b442410              MOVQ 0x10(SP), AX
    0x4805fe              488b4c2418              MOVQ 0x18(SP), CX
    0x480603              4889442430              MOVQ AX, 0x30(SP)
    0x480608              48894c2438              MOVQ CX, 0x38(SP)
    0x48060d              488b6c2420              MOVQ 0x20(SP), BP
    0x480612              4883c428                ADDQ $0x28, SP
    0x480616              c3                      RET
    0x480617              e814a5fcff              CALL runtime.morestack_noctxt(SB)
    0x48061c              eba2                    JMP github.com/knz/go-panic/src.MakeInterface2(SB)
```
什么有发生呢？
这个方法突然变大了很多，因为他在调用另一个函数runtime.convT2T64
这个方法做了什么呢？
```
位于runtime/iface.go
func convT2I64(tab *itab, elem unsafe.Pointer) (i iface) {
     t := tab._type
     // [...]
     var x unsafe.Pointer
     // [...]
             x = mallocgc(8, t, false)
             *(*uint64)(x) = *(*uint64)(elem)
     // [...]
     i.tab = tab
     i.data = x
     return
}
```
实际上是调用堆分配器分配一个槽位用于存储被提供值得副本(即将值进行copy)，并且接口值中将保存一个指向这个槽得指针。
换而言之，一般，通过值类型实现的接口中每次将值类型转化为接口是都是需要去访问堆内存的。
作为特例，如果是使用的是零值来实现的接口，在转换的时候就避免了堆得分配，因为有一个特殊的零值地址来构造接口引用。在上面的代码中这段是有被忽略，请看下面的代码：
```
原文中使用的是go1.10
if *(*uint64)(elem) == 0 {
        x = unsafe.Pointer(&zeroVal[0])
} else {
        x = mallocgc(8, t, false)
        *(*uint64)(x) = *(*uint64)(elem)
}
下面的代码使用的是go1.13
	if val == 0 {
		x = unsafe.Pointer(&zeroVal[0])
	} else {
		x = mallocgc(8, t, false)
		*(*uint64)(x) = val
	}
有些许差别，在1.13中在传入是就已使用的参数类型是uint64
```
这个是正确的因为函数convT2I64只能被用于实现该接口的64位类型。正好我在定义结构体时里面包含了一个64位类型。
有大量的convT2I这类方法，用于不同的类型布局，例如：
- convT2I16,convT2I32,convT2I64为了小类型
- convT2IString，convT2Islice为了string和slice类型
- convT2Inoptr用于不包含指针的结构体
- convT2I为了其他情况
一般情况，除了convT2Inoptr和convT2I其他的在值为0的时候都会避免对分配。
不过，根据上面的情况，构造值类型的接口的调用者是必须去检查堆栈的大小并设置活跃记录，因为他正在进行调用。
可以看到通过值类型实现的接口，在转换的过程中会造成较大的开销。
### 空结构体的接口
特殊情况空结构体。他也可以实现接口并且没有开销。看下面的代码：
```
type empty struct{}
func (empty) foo() {}
var x empty
func MakeInterface3() Foo { return x }
```
汇编代码：
```
MakeInterface3:
  0x4805c0              488d0539020400          LEAQ go.itab.src.empty,src.Foo(SB), AX
  0x4805c7              4889442408              MOVQ AX, 0x8(SP)
  0x4805cc              488d05edf20b00          LEAQ runtime.zerobase(SB), AX
  0x4805d3              4889442410              MOVQ AX, 0x10(SP)
  0x4805d8              c3                      RET
```
通过空结构体实现的接口的值总是使用&runtime.zerobase确定好的因此没有开销。

## error是一个接口类型
对比下面的两个方法：
```
func Simple1()int{ return 123}
func Simple2()(int, error){return 123, nil}
```
汇编代码：
```
Simple1:
  0x4805b0              48c74424087b000000      MOVQ $0x7b, 0x8(SP)
  0x4805b9              c3                      RET

Simple2:
  0x4805c0              48c74424087b000000      MOVQ $0x7b, 0x8(SP)
  0x4805c9              0f57c0                  XORPS X0, X0
  0x4805cc              0f11442410              MOVUPS X0, 0x10(SP)
  0x4805d1              c3                      RET
```
可以看到error是一个接口类型，可以看到这个方法必须使用两个额外的字作为返回值。
为空的时候相对简单(花费了额外的16字节的0数据)
如果err是预先分配好的也是很简单的。例如：
```
var errDivByZero = errors.New("can't divide by zero")

func Compute(x, y float64) (float64, error) {
  if y == 0 {
    return 0, errDivByZero
  }
  return x / y, nil
}
```
汇编代码：
```
Compute:
  0x4805e0              f20f10442410            MOVSD_XMM 0x10(SP), X0 // load y into X0
        if y == 0 {
  0x4805e6              0f57c9                  XORPS X1, X1    // compute float64(0)
  0x4805e9              660f2ec1                UCOMISD X1, X0  // is y == 0?
  0x4805ed              7521                    JNE 0x480610    // no: go to return x/y
  0x4805ef              7a1f                    JP 0x480610     // no: go to return x/y
                return 0, errDivByZero
  0x4805f1              488b05402c0a00          MOVQ src.errDivByZero+8(SB), AX
  0x4805f8              488b0d312c0a00          MOVQ src.errDivByZero(SB), CX
  0x4805ff              f20f114c2418            MOVSD_XMM X1, 0x18(SP)
  0x480605              48894c2420              MOVQ CX, 0x20(SP)
  0x48060a              4889442428              MOVQ AX, 0x28(SP)
  0x48060f              c3                      RET
        return x / y, nil
  0x480610              f20f104c2408            MOVSD_XMM 0x8(SP), X1   // load x into X1
  0x480616              f20f5ec8                DIVSD X0, X1            // compute x / y
  0x48061a              f20f114c2418            MOVSD_XMM X1, 0x18(SP)  // return x / y
  0x480620              0f57c0                  XORPS X0, X0            // compute error(nil)
  0x480623              0f11442420              MOVUPS X0, 0x20(SP)     // return error(nil)
  0x480628              c3                      RET
```
### 通常情况计算错误
预分配的简单情况是有被有效的处理了，但是实际的代码中通常是需要包括一些上下文的信息，例如：
```
func Compute(x, y float64) (float64, error) {
  if y == 0 {
    return 0, fmt.Errorf("can't divide %f by zero", x)
  }
  return x / y, nil
}
```
如果我们以这种方式来组织我们的函数是，我们是要付出一些代码用于调用另一个函数：设置活跃记录，帧指针，堆栈大小检查等，即使没有错误发生(代码不会走到哪里)。
这使得这个真正简单的方法Compute，仅有一条关键指令的除法运算变得很大：
```
Compute:
  // [... stack size check, SP and BP set up elided ...]
  0x482201              f20f10442468            MOVSD_XMM 0x68(SP), X0  // load y
        if y == 0 {   // like before
  0x482207              0f57c9                  XORPS X1, X1   // compute float64(0)
  0x48220a              660f2ec1                UCOMISD X1, X0 // is y == 0?
  0x48220e              0f85a7000000            JNE 0x4822bb   // no: go to return x/y
  0x482214              0f8aa1000000            JP 0x4822bb    // no: go to return x/y

                return 0, fmt.Errorf("can't divide %f by zero", x)
  0x48221a              f20f10442460            MOVSD_XMM 0x60(SP), X0  // load x

  // The following code allocates a special struct using
  // runtime.convT2E64 to pass the variable arguments to
  // fmt.Errorf. The struct contains the value of x.
//分配一个特殊的结构体使用convT2E64将可变参数传递给fmt.Errorf。结构体中包含了x的值
  0x482220              f20f11442438            MOVSD_XMM X0, 0x38(SP)
  0x482226              0f57c0                  XORPS X0, X0
  0x482229              0f11442440              MOVUPS X0, 0x40(SP)
  0x48222e              488d05cbf80000          LEAQ 0xf8cb(IP), AX
  0x482235              48890424                MOVQ AX, 0(SP)
  0x482239              488d442438              LEAQ 0x38(SP), AX
  0x48223e              4889442408              MOVQ AX, 0x8(SP)
  0x482243              e83895f8ff              CALL runtime.convT2E64(SB)
  0x482248              488b442410              MOVQ 0x10(SP), AX
  0x48224d              488b4c2418              MOVQ 0x18(SP), CX

  // The varargs struct is saved for later on the stack.
//可变结构体被保存在堆栈中为了后面的使用
  0x482252              4889442440              MOVQ AX, 0x40(SP)
  0x482257              48894c2448              MOVQ CX, 0x48(SP)

  // The constant string "can't divide..." is passed in the argument list of fmt.Errorf.
// 将常量字符串传到fmt.Errorf中
  0x48225c              488d05111e0300          LEAQ 0x31e11(IP), AX
  0x482263              48890424                MOVQ AX, 0(SP)
  0x482267              48c744240817000000      MOVQ $0x17, 0x8(SP)

  // A slice object is created to point to the vararg struct and given as argument to fmt.Errorf.
// 创建一个切片对象并指向可变结构体并传递给fmt.Errorf
  0x482270              488d442440              LEAQ 0x40(SP), AX
  0x482275              4889442410              MOVQ AX, 0x10(SP)
  0x48227a              48c744241801000000      MOVQ $0x1, 0x18(SP)
  0x482283              48c744242001000000      MOVQ $0x1, 0x20(SP)
  0x48228c              e8cf81ffff              CALL fmt.Errorf(SB)
  // The result value of fmt.Errorf is retrieved.
  0x482291              488b442428              MOVQ 0x28(SP), AX
  0x482296              488b4c2430              MOVQ 0x30(SP), CX

  // return float64(0) as first return value:
  0x48229b              0f57c0                  XORPS X0, X0
  0x48229e              f20f11442470            MOVSD_XMM X0, 0x70(SP)
  // return the result of fmt.Errorf as 2nd return value:
  0x4822a4              4889442478              MOVQ AX, 0x78(SP)
  0x4822a9              48898c2480000000        MOVQ CX, 0x80(SP)
  // [ ... restore BP/SP ... ]
  0x4822ba              c3                      RET

        return x / y, nil    // same as before
  0x4822bb              f20f104c2460            MOVSD_XMM 0x60(SP), X1 // load x into X1
  0x4822c1              f20f5ec8                DIVSD X0, X1           // compute x / y
  0x4822c5              f20f114c2470            MOVSD_XMM X1, 0x70(SP) // return x / y
  0x4822cb              0f57c0                  XORPS X0, X0           // compute error(nil)
  0x4822ce              0f11442478              MOVUPS X0, 0x78(SP)    // return error(nil)
  // [ ... restore BP/SP ... ]
  0x4822dc              c3                      RET
```
因此我们学到了什么？
- fmt.Errorf(和go中的可变参数函数一样)获得额外的参数传递代码:这个参数被存储在调用者的活跃记录中，切片对象作为被调用者的参数被给出。
- 这个代价只有在这个错误实际发生的时候才会被支付。
注意，我们没有考虑到运行fmt.Errorf的成本，通常是要多次进行堆内存的分配因为不知道这个string到底有多长。
### 常见的测试错误
另一个常见的就是当调用者去检查被 调用者的返回的错误时：
```
func Caller() (int, error) {
  v, err := Callee()
  if err != nil {
    return -1, err
  }
  return v + 1, nil
}
```
汇编代码
```
Caller:
  // [... stack size check, SP and BP set up elided ...]
        v, err := Callee()
  0x48061d              e8beffffff              CALL src.Callee(SB)
  0x480622              488b442410              MOVQ 0x10(SP), AX  // retrieve return value
  0x480627              488b0c24                MOVQ 0(SP), CX     // load error vtable
  0x48062b              488b542408              MOVQ 0x8(SP), DX   // load error value
        if err != nil {
  0x480630              4885d2                  TESTQ DX, DX       // is the value part nil?
  0x480633              741d                    JE 0x480652        // yes, go to v+1 below
                return -1, err
  0x480635              48c7442428ffffffff      MOVQ $-0x1, 0x28(SP)  // return -1
  0x48063e              4889542430              MOVQ DX, 0x30(SP)     // return err.vtable
  0x480643              4889442438              MOVQ AX, 0x38(SP)     // return err.value
  // [ ... restore BP/SP ... ]
  0x480651              c3                      RET
        return v + 1, nil
  0x480652              488d4101                LEAQ 0x1(CX), AX    // compute v + 1
  0x480656              4889442428              MOVQ AX, 0x28(SP)   // return v + 1
  0x48065b              0f57c0                  XORPS X0, X0        // compute error(nil)
  0x48065e              0f11442430              MOVUPS X0, 0x30(SP) // return error(nil)
  // [ ... restore BP/SP ... ]
  0x48066c              c3                      RET
```
在任何时候，当调用者需要检查被调用者返回的err，有两步去检索这个错误的值，两步去测试这个err是否为nil，在没有错误的路径中是有两个指令去返回一个nil的。
作为参考（在下面再次查看），如果对错误进行检查或者传递将会简单很多。
```
Caller:
    // [... stack size check, SP and BP set up elided ...]
    0x48060d              e8ceffffff              CALL github.com/knz/go-panic/src.Callee2(SB)
    0x480612              488b0424                MOVQ 0(SP), AX    // retrieve return value
    0x480616              48ffc0                  INCQ AX           // compute v + 1
    0x480619              4889442418              MOVQ AX, 0x18(SP) // return v + 1
    // [ ... restore BP/SP ... ]
    0x480627              c3                      RET
```
(没有额外的指令和分支)
## defer的实现
go提供了一个可以在函数体内注册回调函数列表的特性，保证在函数返回的时候能被调用，即使异常发生时也会被调用。
这个是有用的可以确保资源释放和互斥锁解锁，而且也不管什么有发生在调用期间。
让我们看看是如何工作的？看看下面的例子：
```
func Defer1() int { defer f(); return 123 }
```
汇编代码：
```
Defer1:
    // [... stack size check, SP and BP set up elided ...]

    // Prepare the return value 0. This is set in memory because
    // (theoretically, albeit not in this particular example) the deferred
    // function can access the return value and may do so before it was
    // set by the remainder of the function body.
    0x48208d              48c744242000000000      MOVQ $0x0, 0x20(SP)

    // Prepare the defer by calling runtime.deferproc(0, &f)
    0x482096              c7042400000000          MOVL $0x0, 0(SP)
    0x48209d              488d05f46e0300          LEAQ 0x36ef4(IP), AX
    0x4820a4              4889442408              MOVQ AX, 0x8(SP)
    0x4820a9              e8822afaff              CALL runtime.deferproc(SB)

    // Special check of the return value of runtime.deferproc.
    // In the common case, deferproc returns 0.
    // If a panic is generated by the function body (or one of the callees),
    // and the defer function catches the panic with `recover`, then
    // control will re-return from `deferproc` with value 1.
    0x4820ae              85c0                    TESTL AX, AX
    0x4820b0              7519                    JNE 0x4820cb  // has a panic been caught?

    // Prepare the return value 123.
    0x4820b2              48c74424207b000000      MOVQ $0x7b, 0x20(SP)
    0x4820bb              90                      NOPL
    // Ensure the defers are run.
    0x4820bc              e84f33faff              CALL runtime.deferreturn(SB)
    // [ ... restore BP/SP ... ]
    0x4820ca              c3                      RET

    // We've caught a panic. We're still running the defers.
    0x4820cb              90                      NOPL
    0x4820cc              e83f33faff              CALL runtime.deferreturn(SB)
    // [ ... restore BP/SP ... ]
    0x4820da              c3                      RET
```
看下面：
- 当函数中使用了defer时，代码中就会调用deferproc和deferreturn，因此需要活跃记录，检查堆栈大小和设置帧指针。
- 如果一个方法中使用了defer那么将会调用deferreturn在return之前。
- 实际上回调函数是没有被保存在活跃记录中的；反而deferproc在内部所做的是将回调函数保存在goroutine的header结构体中的链表里面。deferreturn是将链表中的回调函数弹出并运行。
不管defer函数是否包含了recover，代码都是以这种方式生成的。在看下面的：

### 延时关闭
实际的使用中延时函数其实是一个闭包，他可以访问封闭函数的局部变量。例如：
```
func Defer2() (res int) {
    defer func() {
            res = 123
    }()
    return -1
}
```
汇编代码
```
Defer2:
    // [... stack size check, SP and BP set up elided ...]

    // Store the zero value as return value.
    0x48208d              48c744242800000000      MOVQ $0x0, 0x28(SP)

    // Store the frame pointer of Defer2 for use by the deferred closure.
    0x482096              488d442428              LEAQ 0x28(SP), AX
    0x48209b              4889442410              MOVQ AX, 0x10(SP)

    // Call runtime.deferproc(8, &Defer2.func1)
    // Where Defer2.func1 is the code generated for the closure, see below.
    // The closure takes an implicit argument, which is the frame
    // pointer of the enclosing function, where it can peek
    // at the enclosing function's local variables.
    0x4820a0              c7042408000000          MOVL $0x8, 0(SP)
    0x4820a7              488d05b26e0300          LEAQ 0x36eb2(IP), AX
    0x4820ae              4889442408              MOVQ AX, 0x8(SP)
    0x4820b3              e8782afaff              CALL runtime.deferproc(SB)

    // Are we recovering from a panic?
    0x4820b8              85c0                    TESTL AX, AX
    0x4820ba              7519                    JNE 0x4820d5

    // Common path.
    // Set -1 as return value.
    0x4820bc              48c744242800000000      MOVQ $-1, 0x28(SP)
    0x4820c5              90                      NOPL
    // Run the defers.
    0x4820c6              e84533faff              CALL runtime.deferreturn(SB)
    // [ ... restore BP/SP ... ]
    0x4820d4              c3                      RET

    // Recovering from a panic.
    0x4820d5              90                      NOPL
    0x4820d6              e83533faff              CALL runtime.deferreturn(SB)
    // [ ... restore BP/SP ... ]
    0x4820e4              c3                      RET

Defer2.func1:
    // Load the frame pointer of the enclosing function.
    MOVQ    0x8(SP), AX
    // Store the new value into the return value slot of the
    // enclosing function's frame.
    MOVQ    $123, (AX)
    RET
```


































